<!--
  Copyright (C) 2009 Orbeon, Inc.

  This program is free software; you can redistribute it and/or modify it under the terms of the
  GNU Lesser General Public License as published by the Free Software Foundation; either version
  2.1 of the License, or (at your option) any later version.

  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
  without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
  See the GNU Lesser General Public License for more details.

  The full text of the license is available at http://www.gnu.org/copyleft/lesser.html
  -->
<xbl:xbl xmlns:xh="http://www.w3.org/1999/xhtml"
         xmlns:xf="http://www.w3.org/2002/xforms"
         xmlns:xs="http://www.w3.org/2001/XMLSchema"
         xmlns:ev="http://www.w3.org/2001/xml-events"
         xmlns:xi="http://www.w3.org/2001/XInclude"
         xmlns:xxi="http://orbeon.org/oxf/xml/xinclude"
         xmlns:xxf="http://orbeon.org/oxf/xml/xforms"
         xmlns:fr="http://orbeon.org/oxf/xml/form-runner"
         xmlns:saxon="http://saxon.sf.net/"
         xmlns:xbl="http://www.w3.org/ns/xbl"
         xmlns:xxbl="http://orbeon.org/oxf/xml/xbl"
         xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
         xmlns:oxf="http://www.orbeon.com/oxf/processors">

    <xbl:binding id="fr-alert-dialog" element="fr|alert-dialog" xxbl:mode="">
        <xbl:resources>
            <xbl:style src="/xbl/orbeon/alert-dialog/alert-dialog.css"/>
        </xbl:resources>
        <xbl:handlers>
            <xbl:handler event="fr-show xxforms-dialog-open" phase="target">
                <xf:setvalue if="event('message')"           ref="message"  value="event('message')"/>
                <xf:setvalue if="event('context')"           ref="context"  value="event('context')"/>
                <xf:setvalue if="event('positive-targetid')" ref="positive" value="event('positive-targetid')"/>
                <xf:setvalue if="event('negative-targetid')" ref="negative" value="event('negative-targetid')"/>
                <xf:setvalue if="event('neutral-targetid')"  ref="neutral"  value="event('neutral-targetid')"/>
                <xxf:show dialog="dialog" xxbl:scope="inner"/>
            </xbl:handler>
        </xbl:handlers>
        <xbl:template xxbl:transform="oxf:unsafe-xslt">
            <xsl:transform version="2.0">
                <xsl:import href="oxf:/oxf/xslt/utils/xbl.xsl"/>

                <xsl:template match="/*">

                    <xsl:variable name="has-multiple-choices" select="count(fr:positive-choice | fr:negative-choice | fr:neutral-choice) > 1"/>

                    <!-- Once AVTs are supported: close="{exists(fr:neutral-choice) and not ($has-multiple-choices)}"  -->
                    <xxf:dialog xxbl:scope="inner" id="dialog" level="modal" appearance="full" close="{(@close, 'false')[1]}"
                            class="xbl-fr-alert-dialog xbl-fr-alert-dialog-{if ($has-multiple-choices) then 'question' else 'info'}">

                        <xf:model>
                            <xf:instance>
                                <alert>
                                    <message/>
                                    <context/>
                                    <positive/>
                                    <negative/>
                                    <neutral/>
                                </alert>
                            </xf:instance>
                        </xf:model>

                        <xf:var name="local" value="instance()"/>

                        <!-- Set the focus on the first button in the dialog. This is done:
                             a) For usability: so users can press enter to activate the button, or press tab to go to
                                the next button.
                             b) For accessibility, as JAWS only reads the content of the dialog if a form control
                                inside the dialog gets the focus.

                             Setting the focus could be done in XForms, but the code would be more complicated as:
                             a) The button would need to have an id, so it needs to be in the inner scope, while the
                                label inside the button needs to be in outer scope.
                             b) We need code to figure out what the id of the first button is, while in JavaScript we
                                can just refer to "the first button in the dialog".
                        -->
                        <xxf:script ev:event="xxforms-dialog-open">
                            var buttons = this.getElementsByTagName("button");
                            if (buttons.length > 0) buttons[0].focus();
                        </xxf:script>
                        <xf:label xxbl:scope="outer"><xsl:copy-of select="fr:label/(@* | node())"/></xf:label>
                        <xh:div class="xbl-fr-alert-dialog-message">
                            <xsl:choose>
                                <xsl:when test="fr:message">
                                    <!-- Use static message -->
                                    <xf:output value="''" xxbl:scope="outer">
                                        <xf:label><xsl:copy-of select="fr:message/(@* | node())"/></xf:label>
                                    </xf:output>
                                </xsl:when>
                                <xsl:otherwise>
                                    <!-- use dynamic message -->
                                    <xf:output value="''"><xf:label ref="$local/message"/></xf:output>
                                </xsl:otherwise>
                            </xsl:choose>
                        </xh:div>
                        <xh:div class="xbl-fr-alert-dialog-buttons">
                            <xf:group appearance="xxf:internal" xxbl:scope="outer">
                                <!-- Any button click closes the dialog and clears the local data -->
                                <xf:action ev:event="DOMActivate">
                                    <xxf:hide dialog="dialog" xxbl:scope="inner"/>
                                    <xf:setvalue iterate="*" ref="." xxbl:scope="inner"/>
                                </xf:action>
                                <xsl:variable name="buttons" as="element(xf:trigger)*">
                                    <xsl:apply-templates/>
                                </xsl:variable>
                                <xsl:for-each select="$buttons">
                                    <xsl:variable name="is-primary" select="position() = last()"/>
                                    <xsl:copy>
                                        <xsl:if test="$is-primary">
                                            <xsl:attribute name="appearance">xxf:primary</xsl:attribute>
                                        </xsl:if>
                                        <xsl:copy-of select="@* | node()"/>
                                    </xsl:copy>
                                </xsl:for-each>
                            </xf:group>
                        </xh:div>
                    </xxf:dialog>
                </xsl:template>

                <xsl:template match="fr:positive-choice">
                    <!-- TODO: i18n -->
                    <xsl:variable name="default"><xh:i class="icon-ok"/> Yes</xsl:variable>
                    <xsl:copy-of select="xf:trigger(@xxf:modal | *, $default, 'positive')"/>
                </xsl:template>

                <xsl:template match="fr:negative-choice">
                    <!-- TODO: i18n -->
                    <xsl:variable name="default"><xh:i class="icon-remove"/> No</xsl:variable>
                    <xsl:copy-of select="xf:trigger(@xxf:modal | *, $default, 'negative')"/>
                </xsl:template>

                <xsl:template match="fr:neutral-choice">
                    <!-- TODO: i18n -->
                    <xsl:copy-of select="xf:trigger(@xxf:modal | *, 'OK', 'neutral')"/>
                </xsl:template>

                <xsl:function name="xf:trigger" as="element(xf:trigger)">
                    <xsl:param name="content" as="node()*"/>
                    <xsl:param name="default" as="item()*"/>
                    <xsl:param name="handler" as="xs:string"/>
                    <xsl:variable name="fr-label" as="element(fr:label)?" select="$content/self::fr:label"/>
                    <xf:trigger xxbl:scope="outer">
                        <xsl:copy-of select="$content except $fr-label"/>
                        <xf:label>
                            <xsl:choose>
                                <xsl:when test="exists($fr-label)">
                                    <!-- We used to use an <xf:output> wrapper here because we prepended icons above.
                                         This change broke the case where there was a non-default static label. Now we
                                         pass <xh:i> via $default instead. It's not ideal that we need to hardcode
                                         <xh:i> and it would be better if we could revert to using CSS for this.
                                         See: https://github.com/orbeon/orbeon-forms/issues/607 -->
                                    <xsl:copy-of select="$fr-label/(@* | node())"/>
                                </xsl:when>
                                <xsl:otherwise>
                                    <xsl:copy-of select="$default"/>
                                </xsl:otherwise>
                            </xsl:choose>
                        </xf:label>
                        <!-- If a custom target is provided, dispatch to it -->
                        <xf:action ev:event="DOMActivate">
                            <xf:var name="local"><xxf:value value="$local" xxbl:scope="inner"/></xf:var>
                            <xf:var name="targetid" value="normalize-space($local/{$handler})"/>

                            <xf:dispatch if="$targetid != ''" name="fr-{$handler}" targetid="{{$targetid}}">
                                <xf:property name="context" value="$local/context"/>
                            </xf:dispatch>
                        </xf:action>
                    </xf:trigger>
                </xsl:function>

                <xsl:template match="@*|node()" priority="-100"/>
            </xsl:transform>
        </xbl:template>
    </xbl:binding>

</xbl:xbl>