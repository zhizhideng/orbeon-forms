<!--
  Copyright (C) 2010 Orbeon, Inc.

  This program is free software; you can redistribute it and/or modify it under the terms of the
  GNU Lesser General Public License as published by the Free Software Foundation; either version
  2.1 of the License, or (at your option) any later version.

  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
  without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
  See the GNU Lesser General Public License for more details.

  The full text of the license is available at http://www.gnu.org/copyleft/lesser.html
  -->
<xbl:xbl xmlns:xh="http://www.w3.org/1999/xhtml"
         xmlns:xf="http://www.w3.org/2002/xforms"
         xmlns:xs="http://www.w3.org/2001/XMLSchema"
         xmlns:ev="http://www.w3.org/2001/xml-events"
         xmlns:xxf="http://orbeon.org/oxf/xml/xforms"
         xmlns:saxon="http://saxon.sf.net/"
         xmlns:xbl="http://www.w3.org/ns/xbl"
         xmlns:xxbl="http://orbeon.org/oxf/xml/xbl"
         xmlns:fr="http://orbeon.org/oxf/xml/form-runner"
         xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
         xmlns:oxf="http://www.orbeon.com/oxf/processors"
         xmlns:exf="http://www.exforms.org/exf/1-0">

    <xbl:script type="text/javascript" src="/ops/yui/element/element.js"/>
    <xbl:script src="/xbl/orbeon/tabview/tabview.js"/>
    <xbl:script src="/ops/yui/tabview/tabview-min.js"/>

    <xbl:binding id="fr-tabview" element="fr|tabview" xxbl:mode="binding">
        <xbl:resources>
            <xbl:style src="/ops/yui/tabview/assets/skins/sam/tabview.css"/>
            <xbl:style src="/xbl/orbeon/tabview/tabview.css"/>
        </xbl:resources>
        <xbl:handlers>
            <xbl:handler event="xforms-focus" phase="target" defaultAction="cancel">
                <!-- Override default behavior and focus on content-->
                <xf:setfocus control="fr-tabview-content"/>
            </xbl:handler>
        </xbl:handlers>
        <xbl:implementation>
            <!-- Local model -->
            <xf:model id="tabview-model">
                <xf:instance id="tabview-instance">
                    <tabview-instance>
                        <active-tab-position/>
                    </tabview-instance>
                </xf:instance>
            </xf:model>
        </xbl:implementation>
        <xbl:template xxbl:transform="oxf:unsafe-xslt">
            <xsl:transform version="2.0">
                <xsl:import href="oxf:/oxf/xslt/utils/copy.xsl"/>

                <xsl:template match="/*">

                    <!-- Tab group, cannot be internal because accessed with "this" in TabView JS initialization -->
                    <xf:group id="fr-tabview-group">

                        <!-- TODO: This should not be necessary, but we need it until this bug is fixed:
                             https://github.com/orbeon/orbeon-forms/issues/339 -->
                        <xsl:copy-of select="namespace::*"/>
                        <xsl:copy-of select="@class"/>

                        <!-- Initialization event -->
                        <xxf:script id="xf-en" ev:event="xforms-enabled" ev:target="#observer">YAHOO.xbl.fr.TabView.instance(this).init();</xxf:script>

                        <!-- Builds relevance-related variables for each tab -->
                        <xsl:for-each select="fr:tab">
                            <!-- Point to whatever the fr:tab is bound to, if anything -->
                            <xf:var name="fr-tab-binding-{position()}">
                                <xxf:value xxbl:scope="outer" value=".">
                                    <xsl:copy-of select="@model | @context | @ref | @bind"/>
                                </xxf:value>
                            </xf:var>
                            <!--  Is this tab relevant -->
                            <xf:var name="fr-tab-relevance-{position()}"
                                select="exists($fr-tab-binding-{position()}) and exf:relevant($fr-tab-binding-{position()})"/>
                            <xf:var name="fr-tab-relevance-{position()}"
                                select="exists($fr-tab-binding-{position()}) and exf:relevant($fr-tab-binding-{position()})"/>
                        </xsl:for-each>

                        <!-- Contains the relevance for all the tabs as a sequence of boolean values -->
                        <xf:var name="fr-tab-relevances" value="({string-join(
                                for $tab-index in 1 to count(fr:tab) return concat('$fr-tab-relevance-', $tab-index), ',')})"/>
                        <!-- Store index of the first tab which is relevant -->
                        <xf:setvalue ev:event="xforms-enabled" ev:target="#observer" ref="active-tab-position" value="index-of($fr-tab-relevances, true())[1]"/>

                        <xh:div class="yui-navset">
                            <!-- Tab labels -->
                            <xh:ul class="yui-nav">
                                <xsl:for-each select="fr:tab">
                                    <xsl:variable name="label" as="element(fr:label)" select="fr:label[1]"/>
                                    <xh:li class="{{if ($fr-tab-relevance-{position()}) then '' else 'xforms-disabled-subsequent'}}{{
                                                if (active-tab-position = '{position()}') then 'selected' else ''}}{{
                                                if (exf:readonly($fr-tab-binding-{position()})) then ' disabled' else ''}}{
                                                if (exists(@class)) then concat(' ', @class) else ''}" id="nav-{position()}">
                                        <!-- Transform xf:label into xf:output -->
                                        <xh:a href="#tab{position()}"><xh:em>
                                            <xsl:choose>
                                                <!-- Static label, just copy content -->
                                                <xsl:when test="empty($label/(@ref | @bind | @value))">
                                                    <xf:group xxbl:scope="outer" appearance="xxf:internal">
                                                        <xsl:copy-of select="$label/node()"/>
                                                    </xf:group>
                                                </xsl:when>
                                                <!-- Dynamic label, generate xf:output -->
                                                <xsl:otherwise>
                                                    <!-- Put content in outer scope -->
                                                    <xf:output xxbl:scope="outer">
                                                        <xsl:copy-of select="$label/(@model | @context | @ref | @bind | @value | node())"/>
                                                    </xf:output>
                                                </xsl:otherwise>
                                            </xsl:choose>
                                        </xh:em></xh:a>
                                    </xh:li>
                                </xsl:for-each>
                            </xh:ul>
                            <!-- Tab content -->
                            <!-- NOTE: It seems we use a group just so we can do a setfocus on it -->
                            <xf:group class="yui-content" id="fr-tabview-content" xxf:element="xh:div">
                                <xsl:for-each select="fr:tab">
                                    <xsl:variable name="tab-position" select="position()"/>
                                    <!-- This <div> represents the <fr:tab> element. Allow external fr-select and fr-deselect. -->
                                    <xf:group xxbl:scope="outer" xxf:element="xh:div" xxf:external-events="fr-select fr-deselect">
                                        <xsl:copy-of select="@class | @id | @model | @context | @ref | @bind"/>

                                        <!-- Upon receiving fr-toggle, tell JavaScript -->
                                        <xxf:script ev:event="fr-toggle"        ev:target="#observer">YAHOO.xbl.fr.TabView.instance(this).toggle(this);</xxf:script>
                                        <!-- Listen to xforms-readonly and xforms-readwrite for this fr:tab -->
                                        <xxf:script ev:event="xforms-readonly"  ev:target="#observer">YAHOO.xbl.fr.TabView.instance(this).readonly(this);</xxf:script>
                                        <xxf:script ev:event="xforms-readwrite" ev:target="#observer">YAHOO.xbl.fr.TabView.instance(this).readwrite(this);</xxf:script>
                                        <!-- When this fr:tab is newly focused, toggle to it -->
                                        <xxf:script ev:event="DOMFocusIn"       ev:target="#observer">YAHOO.xbl.fr.TabView.instance(this).toggle(this);</xxf:script>

                                        <!-- Client dispatches fr-select upon tab selection -->
                                        <xf:action ev:event="fr-select" ev:target="#observer">
                                            <xf:setvalue xxbl:scope="inner" ref="active-tab-position" value="{$tab-position}"/>
                                        </xf:action>

                                        <!-- Tab content -->
                                        <xsl:apply-templates select="node() except fr:label"/>
                                    </xf:group>
                                </xsl:for-each>
                            </xf:group>
                        </xh:div>
                    </xf:group>
                </xsl:template>
            </xsl:transform>
        </xbl:template>
    </xbl:binding>
</xbl:xbl>
